\documentclass[12pt,a4paper]{article}
\usepackage[utf8]{inputenc}
\usepackage[T1]{fontenc}
\usepackage{xcolor}
\usepackage{graphicx}
\usepackage{listings}
\usepackage{geometry}

\geometry{margin=1in}

\definecolor{background}{rgb}{1.00, 1.00, 1.00}
\definecolor{brightred}{rgb}{0.80, 0.03, 0.16}
\definecolor{brightorange}{rgb}{0.79, 0.28, 0.08}

\lstdefinestyle{custom-style}{
    frame=single,
    upquote=true,
    rulecolor=\color{gray},
    backgroundcolor=\color{background},
    stringstyle=\color{brightred},
    keywordstyle=\color{brightorange},
    numberstyle=\scriptsize\color{gray},
    basicstyle=\ttfamily\footnotesize,
    columns=fullflexible,
    literate=%
        {|}{{{\color{brightorange}|}}}1
        {::=}{{{\color{brightorange}::=}}}3,
    breakatwhitespace=false,
    breaklines=true,
    keepspaces=true,
    numbers=left,
    numbersep=8pt,
    showspaces=false,
    showstringspaces=false,
    showtabs=false,
    tabsize=2
}

\date{}
\title{
    \includegraphics[width=0.67\linewidth]{logo.pdf} \\
    an interpreted imperative programming \\ language that wants to believe
}
\author{Lorenzo Loconte}

\begin{document}
\maketitle

\section*{Introduction}
\textbf{Shrimp} is a very simple and didactic imperative programming language designed during the course of \textit{Formal Methods for Computer Science} at Universita' degli Studi di Bari Aldo Moro.
\textbf{Shrimp} uses an \textit{eager evaluation strategy}.
In order to ensure that, the interpreter executes the code using the \textit{call by value} method.

\section*{Software Modules}
The program is composed by three main components:
\begin{itemize}
    \item The \textbf{parser}
    \item The \textbf{optimizer}
    \item The \textbf{interpreter}
\end{itemize}

The \textbf{parser} takes in input the source code and convert it into an intermediate representation.
The intermediate representation have the structure of a $n$-ary tree having the non-terminals of the grammar as internal nodes and commands, identifiers and integer constants on the leaves.

The \textbf{optimizer} takes in input the intermediate representation given by the parser.
The result of the optimizer is an \textit{optimized} intermediate representation.
It evaluates the constant expressions (both arithmetic and boolean) that might be present in the source code and replace them with the resulting constants.
The optimizer also checks for empty command blocks and useless branch statements and removes (or optimize) them.

The \textbf{interpreter} execute the semantics present in an intermediate representation.
The basic idea is to use a \textbf{state} (or environment) that collects the values of the variables during the execution of the program.
The result of the interpretation is the resulting state, that is a set of ground integer assignments to the variables.

\section*{The Language Syntax}
The syntax for the \textbf{Shrimp} programming language is a context-free grammar.
So, it can be denoted using some kind of EBNF (Extended Backus Naur Form) as following:
\begin{lstlisting}[language=C, style=custom-style]
Integer ::= [0-9]+
Identifier ::= [a-zA-Z_]+ \ Keyword
Program ::= Header "shrimp" Block
Header ::= [VariableDecl]*
Block ::= [Command]*

VariableDecl ::= "let" Identifier "as" Type
Type ::= {"int" | "bool" | "array" "[" Integer "]"}
Command ::= {Assignment | Branch | Loop}

Assignment ::= Identifier
             {"=" ArithmeticExpr | "=" BooleanExpr |
              "[" ArithmeticExpr "]" "=" ArithmeticExpr} ";"

Branch ::= "if" "(" BooleanExpr ")" "then"
           Block ["else" Block] "end" ";"

Loop ::= "while" "(" BooleanExpr ")" "do"
         Block "end" ";"

ArithmeticExpr ::= ArithmeticTerm
                   [{"+" | "-"} ArithmeticTerm]*
ArithmeticTerm ::= ArithmeticFactor
                   [{"*" | "/" | "%"} ArithmeticFactor]*
ArithmeticFactor ::=
    Integer
  | Identifier
  | Identifier "[" ArithmeticExpr "]"
  | "-" ArithmeticExpr
  | "(" ArithmeticExpr ")"

BooleanExpr ::= ArithmeticTerm ["or" ArithmeticTerm]*
BooleanTerm ::= ArithmeticFactor ["and" ArithmeticFactor]*
BooleanFactor ::=
    "true"
  | "false"
  | Identifier
  | "not" BooleanExpr
  | ArithmetciExpr "eq" ArithmeticExpr
  | ArithmetciExpr "neq" ArithmeticExpr
  | ArithmetciExpr "lt" ArithmeticExpr
  | ArithmetciExpr "gt" ArithmeticExpr
  | ArithmetciExpr "leq" ArithmeticExpr
  | ArithmetciExpr "geq" ArithmeticExpr
  | "(" BooleanExpr ")"
\end{lstlisting}
Some of the non-terminals of this context-free grammar are reported directly in Haskell.
That is, I defined an \textit{abstract syntax tree} that also represents the intermediate representation of a program.
This intermediate representation will be the result of the parser.
Moreover, the presence of an intermediate representation permits us to apply optimizations \textit{at prior} respect to the interpretation step.
The following code snippet contains the definition of the \textit{abstarct syntax tree}.
\begin{lstlisting}[language=Haskell, style=custom-style]
type Program = (Header, Block)
type Block = [Command]
type Header = [Variable]

data ArithmeticExpr
  = Constant Int
  | IntegerVar String
  | ArrayVar String ArithmeticExpr
  | Add ArithmeticExpr ArithmeticExpr
  | Sub ArithmeticExpr ArithmeticExpr
  | Mul ArithmeticExpr ArithmeticExpr
  | Div ArithmeticExpr ArithmeticExpr
  | Mod ArithmeticExpr ArithmeticExpr
  | Neg ArithmeticExpr
  deriving (Eq, Show)

data BooleanExpr
  = Truth Bool
  | BooleanVar String
  | Not BooleanExpr
  | Or BooleanExpr BooleanExpr
  | And BooleanExpr BooleanExpr
  | Equal ArithmeticExpr ArithmeticExpr
  | NotEqual ArithmeticExpr ArithmeticExpr
  | Less ArithmeticExpr ArithmeticExpr
  | Greater ArithmeticExpr ArithmeticExpr
  | LessEqual ArithmeticExpr ArithmeticExpr
  | GreaterEqual ArithmeticExpr ArithmeticExpr
  deriving (Eq, Show)

data Command
  = Skip
  | ArithmeticAssignment String ArithmeticExpr
  | BooleanAssignment String BooleanExpr
  | ArrayAssignment String ArithmeticExpr ArithmeticExpr
  | Branch BooleanExpr Block Block
  | Loop BooleanExpr Block
  deriving (Eq, Show)

data Variable
  = IntegerDecl String
  | BooleanDecl String
  | ArrayDecl String Int
  deriving (Eq, Show)
\end{lstlisting}

\section*{The Parser}
The parser can be viewed as a function from a string to a list of pairs of values and strings (Graham Hutton).
\begin{lstlisting}[language=Haskell, style=custom-style]
newtype Parser a = Parser {unwrap :: String -> [(a, String)]}
\end{lstlisting}
Note that the parser have a special function called \texttt{unwrap} that takes the function out from the parser.
The parser is implemented in a ``monadic'' way.
That is, I implemented the following interfaces: \textit{monad plus} and \textit{monad alternative} as suggested in \textit{Monadic Parsing in Haskell} (Hutton \& Meijer).
In order to implement the monad interface I also implemented the \textit{functor}, \textit{applicative} and \textit{monad} interfaces.
\begin{lstlisting}[language=Haskell, style=custom-style]
instance Functor Parser where
  fmap f p = Parser (\cs ->
    [(f a, cs') | (a, cs') <- unwrap p cs])
\end{lstlisting}
The functor implementation for the parser implements the \texttt{fmap} function, that is the application of a function on a wrapped parser.
\begin{lstlisting}[language=Haskell, style=custom-style]
instance Applicative Parser where
  pure a = Parser (\cs -> [(a, cs)])
  p <*> q = Parser (\cs -> concat
    [unwrap (fmap a q) cs' | (a, cs') <- unwrap p cs])
\end{lstlisting}
Moreover, the applicative implementation for the parser introduces both the function \texttt{pure} and the operator \texttt{<*>}.
The \texttt{pure} function takes a simple value and wraps it into a parser.
The \texttt{<*>} operator takes in input a function wrapped in a parser and another parser.
The result is the application of the wrapped function onto the parser.
\begin{lstlisting}[language=Haskell, style=custom-style]
instance Monad Parser where
  return a = pure a
  p >>= f = Parser (\cs -> concat
    [unwrap (f a) cs' | (a, cs') <- unwrap p cs])
\end{lstlisting}
The \texttt{bind} operator \texttt{>>=} takes as input a parser and a function that returns a parser.
The result of this operator is the application of the function to the ``unwrapped'' parser.
The application of multiple \texttt{bind} nested operations can be abbreviated using the \texttt{do} construct in Haskell.

Moreover, two other \textit{custom} interfaces are implemented, needed for making the parser: \texttt{MonadPlus} and \texttt{MonadAlternative}.
The \texttt{plus} operator defined on parsers concatenates the result of each one.
Moreover, the \texttt{MonadAlternative} give us an operator \texttt{<|>} useful for combining parsers in a mutually exclusive way.
The classes definition can be found in the following code snippet.
\begin{lstlisting}[language=Haskell, style=custom-style]
class (Monad m) => MonadPlus m where
  zero :: m a
  plus :: m a -> m a -> m a

class (MonadPlus m) => MonadAlternative m where
  (<|>) :: m a -> m a -> m a
  many :: m a -> m [a]
  many m = some m <|> return []
  some :: m a -> m [a]
  some m = liftA2 (:) m (many m)
  chain :: m a -> m (a -> a -> a) -> m a
  chain p o = do a <- p; rest a
    where
      rest a = (do f <- o; a' <- p; rest (f a a')) <|> return a
\end{lstlisting}
Note that the function \texttt{liftA2} is the composition of \texttt{fmap} and the applicative operator \texttt{<*>}.
The functions \texttt{many} and \texttt{some} are called \textit{combinators} and are used to define the concept of repeated parsing.
The \texttt{many} function refers to zero to any number of applications of a parser while the \texttt{some} function refers to at least one to any number of applications of a parser.
The implementation for the parser of the \texttt{bind} operator and the other functions described before is the following.
The \texttt{chain} function is another combinator that it's used for left-associative recursion on parsers, as described by Hutton \& Meijer.
\begin{lstlisting}[language=Haskell, style=custom-style]
instance MonadPlus Parser where
  zero = Parser (const [])
  p `plus` q = Parser (\cs -> unwrap p cs ++ unwrap q cs)

instance MonadAlternative Parser where
(<|>) p q = Parser (\cs ->
  case unwrap (p `plus` q) cs of
    [] -> []
    (x : _) -> [x])
\end{lstlisting}
The \texttt{zero} function defines what is an empty parser.
Note that in this context an empty parser is a \textit{failed} parser, that is a parser that results from a syntactical error.
The \texttt{plus} function concatenates the results of two individual parsers.
Using the monadic definition of parser, it permits us to easily build a parser for each structure of the programming language, without having to handle the eventual parsing errors individually.
For example, it's possible to define a parser for symbols, identifiers and integers as in the following code snippet.
Moreover, since the parser implements the \texttt{MonadAlternative} interface described above, the combination of multiple parsers is straightforward and easily \textit{parallelizable} by the compiler.
\begin{lstlisting}[language=Haskell, style=custom-style]
item :: Parser Char
item = Parser (\case "" -> []; (c : cs) -> [(c, cs)])

satisfy :: (Char -> Bool) -> Parser Char
satisfy p = do c <- item; if p c then return c else zero

token :: Parser a -> Parser a
token p = do space; v <- p; space; return v

space :: Parser String
space = many $ satisfy isSpace

identifier :: Parser String
identifier = do
  s <- token $ some $ satisfy isLetter
  if s `elem` keywords
    then zero
    else return s

constant :: Parser Int
constant = read <$> token (some $ satisfy isDigit)

char :: Char -> Parser Char
char c = satisfy (c ==)
\end{lstlisting}
That is, I firstly defined a \texttt{item} function that reads a character from the string.
Then I defined a function called \texttt{satisfy} that apply a constraint to the character read by the parser.
This function returns an \texttt{zero} parser if the constraint is not satisfied.
In the end, using both the combinators \texttt{many} and \texttt{some}, I built parsers for identifiers, constants and also spaces that are the main blocks of the context-free grammar already defined.
In the same way, it's possible to define a parser for other constructs, such as keywords and symbols.
It's important to note that the identifier parser fails if a keyword is found.
This is a very fundamental aspect for a programming language.
So, it's not possible use variables identified by keywords. \\
\begin{lstlisting}[language=Haskell, style=custom-style]
keyword :: String -> Parser String
keyword cs = token $ word cs

word :: String -> Parser String
word [c] = do char c; return [c]
word (c : cs) = do char c; word cs; return (c : cs)

symbol :: Char -> Parser Char
symbol c = token $ char c
\end{lstlisting}
In order to build parsers for both arithmetic and boolean expression the \texttt{<|>} operator between parsers is used.
So, I defined the parser for arithmetic expressions using other sub-parsers that are used in order to maintain the operators precedence.
Moreover, I used the \texttt{chain} combinator in order to build the \textit{abstract syntax tree} by the left-associative operators of the grammar.
\begin{lstlisting}[language=Haskell, style=custom-style]
arithmeticExpr :: Parser ArithmeticExpr
arithmeticExpr = chain arithmeticTerm op
  where
    op =
      do symbol '+'; return Add
        <|> do symbol '-'; return Sub

arithmeticTerm :: Parser ArithmeticExpr
arithmeticTerm = chain arithmeticFactor op
  where
    op =
      do symbol '*'; return Mul
        <|> do symbol '/'; return Div
        <|> do symbol '%'; return Mod

arithmeticFactor :: Parser ArithmeticExpr
arithmeticFactor =
  do Constant <$> constant
    <|> do
      d <- identifier
      do
        symbol '['
        k <- arithmeticExpr
        symbol ']'
        return (ArrayVar d k)
        <|> return (IntegerVar d)
    <|> do symbol '-'; Neg <$> arithmeticExpr
    <|> do symbol '('; a <- arithmeticExpr; symbol ')'; return a
\end{lstlisting}
The \texttt{chain} combinator is used in both \texttt{arithmeticExpr} and \texttt{arithmeticTerm} in order to permits to concatenate multiple addition/subtraction operators and multiple multiplication/division/modulus operators.
If we didn't used the \texttt{chain} combinator but simple plain tail recursion we would have that the order of operations would be from right to left instead of left to right.
The parser for boolean expressions is similar to the parser for arithmetic expressions and so it's omitted in this documentation.

In the end we have a parser for every command described in the grammar.
It's important to notice that the grammar also allows for \texttt{if-then} statements (i.e. without the \texttt{else} command block).
A parser that works on both \texttt{if-then-else} and \texttt{if-then} statements is implemented by combining the two individual parsers using the \texttt{<|>} operator.
Moreover, using the \texttt{many} combinator and the \texttt{<|>} operator, the parsers for both multiple commands and a single command are straightforward.
\begin{lstlisting}[language=Haskell, style=custom-style]
assignment :: Parser Command
assignment = do
  d <- identifier
  do
    symbol '='
    a <- arithmeticExpr
    symbol ';'
    return (ArithmeticAssignment d a)
    <|> do
      symbol '='
      b <- booleanExpr
      symbol ';'
      return (BooleanAssignment d b)
    <|> do
      symbol '['
      k <- arithmeticExpr
      symbol ']'
      symbol '='
      a <- arithmeticExpr
      symbol ';'
      return (ArrayAssignment d k a)

branch :: Parser Command
branch = do
  keyword "if"
  symbol '('
  b <- booleanExpr
  symbol ')'
  keyword "then"
  c1 <- block
  do
    keyword "else"
    c2 <- block
    keyword "end"
    symbol ';'
    return (Branch b c1 c2)
  <|> do
    keyword "end"
    symbol ';'
    return (Branch b c1 [Skip])

loop :: Parser Command
loop = do
  keyword "while"
  symbol '('
  b <- booleanExpr
  symbol ')'
  keyword "do"
  c <- block
  keyword "end"
  symbol ';'
  return (Loop b c)

skip :: Parser Command
skip = do keyword "skip"; symbol ';'; return Skip
\end{lstlisting}
Another parser that is needed for the grammar, is the parser for the variable declarations.
That is, in this programming language we can declare variables only on the top of the program, and before the \texttt{shrimp} keyword.
The variables can be integers, boolean or array of integers.
For the declaration of array of integers, the size of the array must be fixed and a constant integer value.
\begin{lstlisting}[language=Haskell, style=custom-style]
variable :: Parser Variable
variable = do
  keyword "let"
  d <- identifier
  keyword "as"
  do
    keyword "int"
    symbol ';'
    return (IntegerDecl d)
    <|> do
      keyword "bool"
      symbol ';'
      return (BooleanDecl d)
    <|> do
      keyword "array"
      symbol '['
      n <- constant
      symbol ']'
      symbol ';'
      return (ArrayDecl d n)
\end{lstlisting}
The parser for the entire program is defined as in the following code snippet.
That is, the parser for a block of commands is defined using the \texttt{many} combinator on the parser for a \texttt{command}.
In the same way, the parser for a header is defined using the \texttt{many} combinator on the parser for a \texttt{variable}.
The parser for the entire parser is defined using both the parser for the header and the command block.
Note that the keyword \texttt{shrimp} separates these parts of the program.
\begin{lstlisting}[language=Haskell, style=custom-style]
program :: Parser Program
program = do
  h <- header
  keyword "shrimp"
  symbol ';'
  b <- block
  return (h, b)

block :: Parser Block
block = many command

header :: Parser Header
header = many variable

parse :: String -> Result (Program, String)
parse cs = case unwrap program cs of
  [] -> Error EmptyProgram
  [(p, cs)] -> Ok (p, cs)
\end{lstlisting}

\section*{The Optimizer}
The \textit{optimization} process is an intermediate step between the parsing and the interpretation of the program itself.
Currently, the main optimization step is related to the execution of constant values.
That is, if an expression in a loop is defined only on constant values, it's better to optimize the computation of that expression by replacing it with the result.
This procedure is done before the interpretation of the program.
For example, consider the following arithmetic expression, expressed in intermediate representation, that we wish to optimize.
If we apply the optimization step to this arithmetic expression, we obtain the equivalent but more efficient arithmetic expression.
\begin{lstlisting}[language=Haskell, style=custom-style]
let expr = Div (
    (Mul (Identifier "x") (Sub (Constant 5) (Constant 3)))
    (Add (Constant 9) (Constant 1)))
\end{lstlisting}
\begin{lstlisting}[language=Haskell, style=custom-style]
let expr' = Div (Mul (Identifier "x") (Constant 2)) (Constant 10)
\end{lstlisting}
The implementation of the \textit{optimization} process in Haskell is straightforward, due to simple recursion functions.
A very similar optimization process is also implemented on boolean expressions.
The \textit{optimization} process also includes a basic optimization on commands such as \texttt{skip}, \texttt{if-then-else} and \texttt{while-do}.
First of all, all the \texttt{skip} commands are removed from the intermediate representation.
Moreover, if the condition of a \texttt{if-then-else} command is always \textit{true} then the entire statement is replaced with the first block of commands.
In a similar way, if the condition is always \textit{false} then the entire statement is replaced with the second block of commands.
\begin{lstlisting}[language=Haskell, style=custom-style]
let command = Branch (Equal (Sub (Constant 1) (Constant 1)) 0)
              [Assignment "x" (Constant 1)]
              [Assignment "x" (Constant 2)]
\end{lstlisting}
\begin{lstlisting}[language=Haskell, style=custom-style]
let command' = [Assignment "x" (Constant 1)]
\end{lstlisting}
Furthermore, this approach is also used for \texttt{while-do} commands.
That is, if the condition of a \texttt{while-do} command is always \textit{false} then the entire statement is completely removed.
\begin{lstlisting}[language=Haskell, style=custom-style]
let command = Loop (Equal (Sub (Constant 1) (Constant 1)) 1)
              [Assignment "x" (Add (IntegerVar "x") (Constant 1))]
\end{lstlisting}
\begin{lstlisting}[language=Haskell, style=custom-style]
let command' = []
\end{lstlisting}
However, if the condition is always \textit{true} then an exception named \textit{Infinite Loop} is raised.
In other words, the optimizer is capable of detecting trivial infinite loops and prevents the interpretation of such programs.
For example, the following loop program's intermediate representation is not interpreted.
That is, an \textit{Infinite Loop} exception is raised during the optimization step.
\begin{lstlisting}[language=Haskell, style=custom-style]
let command = Loop (Equal (Sub (Constant 1) (Constant 1)) 0)
              [Assignment "x" (Add (IntegerVar "x") (Constant 1))]
\end{lstlisting}

\clearpage

\section*{The Interpreter}
Finally, the obtained intermediate representation is interpreted by the interpreter.
The interpreter scan the intermediate representation using \textit{depth-first traversal} on the $n$-ary tree that compose the intermediate representation itself.
Before introducing the execution of the interpreter on an intermediate representation, some useful data types are defined in order to handle errors.
\begin{lstlisting}[language=Haskell, style=custom-style]
data Exception
  = EmptyProgram
  | InfiniteLoop
  | DivisionByZero
  | UndeclaredVariable String
  | MultipleVariable String
  | TypeMismatch String
  | OutOfBound String Int
  | InvalidSize String

instance Show Exception where
  show EmptyProgram = "Empty Program"
  show InfiniteLoop = "Infinite Loop"
  show DivisionByZero = "Division By Zero"
  show (UndeclaredVariable d) = "Undeclared Variable" ++ ": " ++ d
  show (MultipleVariable d) = "Multiple Variable" ++ ": " ++ d
  show (TypeMismatch d) = "Type Mismatch" ++ ": " ++ d
  show (OutOfBound d i) = "Out Of Bound" ++ ": " ++ d ++ " at " ++ show i
  show (InvalidSize d) = "Invalid Size" ++ ": " ++ d

data Result a = Ok a | Error Exception

instance Functor Result where
  fmap f (Ok v) = Ok (f v)
  fmap _ (Error e) = Error e

instance Applicative Result where
  pure v = Ok v
  (<*>) (Ok f) (Ok v) = Ok (f v)
  (<*>) (Error e) _ = Error e
  (<*>) _ (Error e) = Error e

instance Monad Result where
  (>>=) (Ok v) f = f v
  (>>=) (Error e) _ = Error e

exception :: Exception -> a
exception e = errorWithoutStackTrace $ show e
\end{lstlisting}
As one can see, the \texttt{Result} data type is a polymorphic type that can be either a \texttt{Ok} or a \texttt{Error}.
The \texttt{Error} type also encapsulate an exception, one of the listed above.
Moreover functor, applicative and monad interfaces are implemented in order to apply the needed operators directly on intermediate results.
The functor, applicative and monad interfaces implementation is as in the \texttt{Maybe} data type.
In the end, the \texttt{exception} function is useful in order to print the error and stop the program without printing the stack trace.

In order to interpret the intermediate representation, two sub-interpreters are implemented.
The first interpreter loads all the variables in the variables declaration section of the program.
The result of the variables loading is a consistent state, viewed as a dictionary with key as the identifier of a variable and value the effective value of the variable at a certain point of the program.
The value of a variable can be either an integer, a boolean value or a vector of integers.
\begin{lstlisting}[language=Haskell, style=custom-style]
initialize :: State -> Header -> State
initialize s [] = s
initialize s ((IntegerDecl d) : hs) =
  case search d s of
    Just _ -> exception (MultipleVariable d)
    Nothing -> initialize s' hs
      where
        s' = insert d (IntegerValue 0) s
initialize s ((BooleanDecl d) : hs) =
  case search d s of
    Just _ -> exception (MultipleVariable d)
    Nothing -> initialize s' hs
      where
        s' = insert d (BooleanValue False) s
initialize s ((ArrayDecl d n) : hs) =
  case search d s of
    Just _ -> exception (MultipleVariable d)
    Nothing ->
      if n > 0
        then initialize s' hs
        else exception (InvalidSize d)
      where
        s' = insert d (ArrayValue (zeroArray n)) s
\end{lstlisting}
The second interpreter executes the commands specified in the commands section of the command (after the \texttt{shrimp} keyword).
So, it interprets every command specified in the grammar, that are \texttt{skip}, various kinds of \texttt{assignment}s, \texttt{if-then-else} and \texttt{if-then} statements and the \texttt{while-do} statement.
\begin{lstlisting}[language=Haskell, style=custom-style]
execute :: State -> Block -> State
execute s [] = s
execute s (Skip : cs) = execute s cs
execute s ((ArithmeticAssignment d a) : cs) =
  case search d s of
    Just (IntegerValue _) ->
      case evalArithmetic s a of
        Ok v -> execute s' cs
          where
            s' = insert d (IntegerValue v) s
        Error e -> exception e
    Just (BooleanValue _) -> exception (TypeMismatch d)
    Just (ArrayValue _) -> exception (TypeMismatch d)
    Nothing -> exception (UndeclaredVariable d)
execute s ((BooleanAssignment d b) : cs) =
  case search d s of
    Just (BooleanValue _) ->
      case evalBoolean s b of
        Ok t -> execute s' cs
          where
            s' = insert d (BooleanValue t) s
        Error e -> exception e
    Just (IntegerValue _) -> exception (TypeMismatch d)
    Just (ArrayValue _) -> exception (TypeMismatch d)
    Nothing -> exception (UndeclaredVariable d)
execute s ((ArrayAssignment d k a) : cs) =
  case search d s of
    Just (ArrayValue vs) ->
      case (evalArithmetic s a, evalArithmetic s k) of
        (Ok v, Ok i) -> execute s' cs
          where
            s' = insert d (ArrayValue vs') s
            vs' = case writeArray i v vs of
              Just vs' -> vs'
              Nothing -> exception (OutOfBound d i)
        (Error e, _) -> exception e
        (_, Error e) -> exception e
    Just (IntegerValue _) -> exception (TypeMismatch d)
    Just (BooleanValue _) -> exception (TypeMismatch d)
    Nothing -> exception (UndeclaredVariable d)
execute s ((Branch b cs' cs'') : cs) =
  case evalBoolean s b of
    Ok True -> execute s (cs' ++ cs)
    Ok False -> execute s (cs'' ++ cs)
    Error e -> exception e
execute s (c@(Loop b cs') : cs) =
execute s (Branch b (cs' ++ [c]) [Skip] : cs)
\end{lstlisting}
The implementation of the evaluation of arithmetic expressions make \textit{heavily} use of the methods exposed by the applicative interface.
Moreover, the \texttt{liftA2} function is used in order to make the evaluation functions implementation more compact and readable.
It's important to notice that arithmetic expressions evaluation includes reading from variables and accessing to arrays by an index that is the evaluation of another arithmetic expression.
Moreover, the current implementation includes the evaluation of the \texttt{minus} unary operator.
In this implementation, I also used the functions \texttt{safeDiv}, \texttt{safeMod} and \texttt{seqM2}.
In other words, the \texttt{seqM2} is a custom function that implements binary sequencing, similarly to the \texttt{bind} operator.
\begin{lstlisting}[language=Haskell, style=custom-style]
evalArithmetic :: State -> ArithmeticExpr -> Result Int
evalArithmetic _ (Constant v) = Ok v
evalArithmetic s (IntegerVar d) =
  case search d s of
    Just (IntegerValue v) -> Ok v
    Just (BooleanValue _) -> exception (TypeMismatch d)
    Just (ArrayValue _) -> exception (TypeMismatch d)
    Nothing -> exception (UndeclaredVariable d)
evalArithmetic s (ArrayVar d k) =
  case search d s of
    Just (ArrayValue vs) ->
      case evalArithmetic s k of
        Ok i ->
          case readArray i vs of
            Just v -> Ok v
            Nothing -> Error (OutOfBound d i)
        Error e -> exception e
    Just (IntegerValue _) -> exception (TypeMismatch d)
    Just (BooleanValue _) -> exception (TypeMismatch d)
    Nothing -> exception (UndeclaredVariable d)
evalArithmetic s (Add a1 a2) = liftA2 (+) v1 v2
  where
    v1 = evalArithmetic s a1
    v2 = evalArithmetic s a2
evalArithmetic s (Sub a1 a2) = liftA2 (-) v1 v2
  where
    v1 = evalArithmetic s a1
    v2 = evalArithmetic s a2
evalArithmetic s (Mul a1 a2) = liftA2 (*) v1 v2
  where
    v1 = evalArithmetic s a1
    v2 = evalArithmetic s a2
evalArithmetic s (Div a1 a2) = seqM2 safeDiv v1 v2
  where
    v1 = evalArithmetic s a1
    v2 = evalArithmetic s a2
evalArithmetic s (Mod a1 a2) = seqM2 safeMod v1 v2
  where
    v1 = evalArithmetic s a1
    v2 = evalArithmetic s a2
evalArithmetic s (Neg a) = negate <$> v
  where
    v = evalArithmetic s a
\end{lstlisting}
The implementations of \texttt{saveDiv}, \texttt{safeMod}, and \texttt{seqM2} functions are listed in the following code snippet.
Note that \texttt{saveDiv} and \texttt{safeMod} are special functions that handles divisions by zero at runtime.
\begin{lstlisting}[language=Haskell, style=custom-style]
safeDiv :: Int -> Int -> Result Int
safeDiv _ 0 = Error DivisionByZero
safeDiv u v = Ok (div u v)

safeMod :: Int -> Int -> Result Int
safeMod _ 0 = Error DivisionByZero
safeMod u v = Ok (mod u v)

seqM2 :: (Monad m) => (a -> b -> m c) -> m a -> m b -> m c
seqM2 f x y = join $ liftA2 f x y

join :: (Monad m) => m (m a) -> m a
join m = m >>= id
\end{lstlisting}
The implementation of the evaluation of boolean expressions is very similar to the one for arithmetic expressions, so it's omitted.

\section*{Conclusion}
The monadic implementation of the parser give us a very simple way of concatenating and combining multiple parsers of sub-grammars.
Also, it's very easy to extend the grammar of the language in order to include other commands or statements.
Moreover, the use of an intermediate representation permits us to apply post-processing and optimizations and make the interpretation itself straightforward and more efficient.

Future works may include the introduction of other types for variables.
Another extension of this work consists of adding useful information about the parsing errors, i.e. missing tokens and relevant row and column locations in the source code where the error occurred.
However, other improvements can be done in the optimization step.
That is, one can implement more ``aggressive'' optimizations of the intermediate representation based on the expected state of memory of the program during its execution.

\clearpage

\section*{Running an example}
First of all, navigate to the project directory.
Then, open \texttt{ghci} and load the needed modules as following.
\begin{lstlisting}[style=custom-style]
:load Main.hs Shrimp.hs
\end{lstlisting}
Finally, run the main program and insert the path of a source file in the \texttt{examples} directory (for example \texttt{primes.shr}).
\begin{lstlisting}[style=custom-style]
*Main> main
The Shrimp Interpreter
Insert the path of the source file:
examples/primes.shr
Memory state:
    "n": int = 49
    "i": int = 15
    "j": int = 0
    "stop": bool = False
    "primes": array[15] = [2,3,5,7,11,13,17,19,23,29,31,37,41,43,47]
\end{lstlisting}

\end{document}
